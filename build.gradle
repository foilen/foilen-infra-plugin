buildscript {
  repositories {
    mavenLocal()
    jcenter()
  }
}

plugins {
  id "com.jfrog.bintray" version "$bintrayVersion"
  id 'io.spring.dependency-management' version "$springDependencyManagementVersion"
}

allprojects {

  apply plugin: 'eclipse'

  group = 'com.foilen'
  version = System.getenv('VERSION')?:'master-SNAPSHOT'

}

subprojects {

  apply plugin: 'com.jfrog.bintray'
  apply plugin: 'io.spring.dependency-management'
  apply plugin: 'maven'
  apply plugin: 'maven-publish'

  apply plugin: 'java'

  repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
  }

  sourceCompatibility = 1.11

  dependencyManagement {
    imports {
      mavenBom "org.springframework.boot:spring-boot-starter-parent:$springBootVersion"
    }
  }

  eclipse {
    classpath {
      downloadSources = true
      downloadJavadoc = true
    }
  }

  task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
  }

  artifacts {
    archives sourcesJar
    archives javadocJar
  }
  
  test {
    useJUnitPlatform()
  }

}

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')

  publish = true

  pkg {
    repo = 'maven'
    userOrg = 'foilen'
    name = 'com.foilen:foilen-infra-plugin'
    licenses = ['MIT']
    vcsUrl = 'https://github.com/foilen/foilen-infra-plugin.git'
  }

  publications = []
}
